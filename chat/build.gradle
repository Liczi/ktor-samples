buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://maven.pkg.jetbrains.space/public/p/ktor/eap"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin-multiplatform'

kotlin {
    targets {
        js('frontend') {
            browser {
                testTask {
                    enabled = false
                }

                dceTask {
                    keep 'ktor-ktor-io.\$\$importsForInline\$\$.ktor-ktor-io.io.ktor.utils.io'
                }

                distribution {
                    directory = file("$projectDir/src/backendMain/resources/web")
                }
            }
        }
        jvm('backend') {
            compilations.main {
                tasks.getByName(processResourcesTaskName) {
                    dependsOn(frontendBrowserWebpack)
                }
            }
        }
    }

    sourceSets {
        backendMain {
            dependencies {
//                implementation "io.github.microutils:kotlin-logging-jvm:2.1.20"
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
                implementation "io.ktor:ktor-server-netty:$ktor_eap_version"
                implementation "io.ktor:ktor-server-websockets:$ktor_eap_version"
                implementation "io.ktor:ktor-server-call-logging:$ktor_eap_version"
                implementation "io.ktor:ktor-server-default-headers:$ktor_eap_version"
                implementation "io.ktor:ktor-server-sessions:$ktor_eap_version"
                implementation "ch.qos.logback:logback-classic:1.2.11"
            }
        }

        backendTest {
            dependencies {
                implementation "io.ktor:ktor-server-test-host:$ktor_eap_version"
                implementation "org.jetbrains.kotlin:kotlin-test"
            }
        }

        frontendMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
                implementation "io.ktor:ktor-client-websockets:$ktor_eap_version"
                implementation "io.ktor:ktor-client-js:$ktor_eap_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$kotlinx_version"
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.pkg.jetbrains.space/public/p/ktor/eap"
    }
}

task run(type: JavaExec, dependsOn: [backendJar]) {
    main = "io.ktor.samples.chat.backend.ChatApplicationKt"
    classpath(configurations.backendRuntimeClasspath, backendJar)
    args = []
}

